varnishtest "lookup_fields: Test workspace exhaustion"

server s1 {
	rxreq
	txresp
} -start

shell "cp ${vmod_topsrc}/src/tests/libvmod-geoip2.mmdb ${tmpdir}"

varnish v1 -arg "-p workspace_client=9k" -vcl+backend {
	import geoip2 from "${vmod_topbuild}/src/.libs/libvmod_geoip2.so";
	import std;
	import vtc;

	sub vcl_init {
		new db1 = geoip2.geoip2("${tmpdir}/libvmod-geoip2.mmdb");
	}

	sub vcl_deliver {
		vtc.workspace_alloc(client, -8);
		set resp.http.city = db1.lookup_fields(
		    "long_string",
		    "127.0.0.1");
	}
} -start

logexpect l1 -v v1 -g raw -d 1 {
	expect * 1001	Error	"geoip2.lookup_fields: Out of workspace"
} -start

client c1 {
	txreq
	rxresp
} -run

logexpect l1 -wait
